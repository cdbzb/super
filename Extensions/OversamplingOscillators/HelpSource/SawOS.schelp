TITLE:: SawOS
summary:: Oversampled Sawtooth Oscillator
categories:: UGens>Oscillator
related:: SawBL, SquareBL, TriBL, ImpulseBL, SinOscOS, TriOS, SawOS, SquareOS, VarSawOS, PMOscOS, PM7OS, FM7OS

DESCRIPTION::
An oversampled Sawtooth oscillator using Jatin Chowdhurry's oversampling library from ChowDSP


CLASSMETHODS::

METHOD:: ar

ARGUMENT:: freq
frequency - FM is possible

ARGUMENT:: phase
values are between 0 and 2 or -1 and 1
phase - PM is possible

ARGUMENT:: oversample
OverSampling Index

0 = none, 1 = 2x, 2 = 4x, 3 = 8x, 4 = 16x

ARGUMENT:: mul
mul

ARGUMENT:: add
add


EXAMPLES::

code::
{SawOS.ar(MouseX.kr(-1000,10000), 0, 1, 0.1)}.scope; //still some aliasing

{SawOS.ar(MouseX.kr(-1000,10000), 0, 4, 0.1)}.scope; //real nice

//phase offset works
{SawOS.ar(400, 0)}.plot;
{SawOS.ar(400, -0.5)}.plot;
{SawOS.ar(400, 1)}.plot;

//phase modulation works
{SawOS.ar(0, LFSaw.ar(400))}.plot;

{SawOS.ar(200, SinOsc.ar(400)*Line.ar(0,2,10), 3, 0.1)}.scope;

//Frequency modulation is possible
({SawOS.ar(MouseX.kr(200,4000)+
	(LFSaw.ar(MouseY.kr(200,4000))*3000), 0, 4, 0.2).dup
}.play)


//Extreme Modulation is possible.

({
	var locIn=LocalIn.ar(1);
	var feedbackMul = 5000;
	var mod = feedbackMul*SawBL.ar((MouseY.kr(0, 8000)+(locIn*2000)));

	var car = SawOS.ar(
		(MouseX.kr(1, 8000)
			+(mod)), 0,4);
	LocalOut.ar(car);
	car.dup*0.2
}.scope)
::
