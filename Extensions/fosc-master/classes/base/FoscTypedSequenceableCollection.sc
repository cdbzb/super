/* ------------------------------------------------------------------------------------------------------------
• FoscTypedSequenceableCollection

x = FoscTypedSequenceableCollection([1, 2, 3, 4], Number);
x.collection;
x.items;

FoscTypedSequenceableCollection.dumpInterface
------------------------------------------------------------------------------------------------------------ */
FoscTypedSequenceableCollection : FoscTypedCollection {
    ///////////////////////////////////////////////////////////////////////////////////////////////////////////
    // PUBLIC METHODS: COMPARISON
    ///////////////////////////////////////////////////////////////////////////////////////////////////////////
    /* --------------------------------------------------------------------------------------------------------
    • ==
    
    a = FoscTypedSequenceableCollection([1, 2, 3, 4], Number);
    b = FoscTypedSequenceableCollection([1, 2], Number);
    a == b;
    a == a.copy;
    -------------------------------------------------------------------------------------------------------- */
    /* --------------------------------------------------------------------------------------------------------
    • !=
    
    a = FoscTypedSequenceableCollection([1, 2, 3, 4], Number);
    b = FoscTypedSequenceableCollection([1, 2], Number);
    a != b;
    a != a.copy;
    -------------------------------------------------------------------------------------------------------- */
    ///////////////////////////////////////////////////////////////////////////////////////////////////////////
    // PUBLIC METHODS: ENUMERATION
    ///////////////////////////////////////////////////////////////////////////////////////////////////////////
    /* --------------------------------------------------------------------------------------------------------
    • do

    x = FoscTypedSequenceableCollection([1, 2, 3, 4], Number);
    x.do { |each| (each * 2).postln };
    -------------------------------------------------------------------------------------------------------- */
    /* --------------------------------------------------------------------------------------------------------
    • iter
    
    a = FoscTypedSequenceableCollection([1, 2, 3, 4], Number);
    a = a.iter;
    5.do { a.next.postln };
    -------------------------------------------------------------------------------------------------------- */
    ///////////////////////////////////////////////////////////////////////////////////////////////////////////
    // PUBLIC METHODS: PROPERTIES
    ///////////////////////////////////////////////////////////////////////////////////////////////////////////
    /* --------------------------------------------------------------------------------------------------------
    • includes
    
    a = FoscTypedSequenceableCollection([1, 2, 3, 4], Number);
    a.includes(4);
    -------------------------------------------------------------------------------------------------------- */
    /* --------------------------------------------------------------------------------------------------------
    • isCollection

    a = FoscTypedSequenceableCollection([1, 2, 3, 4], Number);
    a.isCollection;
    -------------------------------------------------------------------------------------------------------- */
    /* --------------------------------------------------------------------------------------------------------
    • items
    
    x = FoscTypedSequenceableCollection([1, 2, 3, 4], Number);
    x.items;
    -------------------------------------------------------------------------------------------------------- */
    /* --------------------------------------------------------------------------------------------------------
    • size
    
    Size of typed sequenceable collection.

    Returns nonnegative integer.
    
    a = FoscTypedSequenceableCollection([1, 2, 3, 4], Number);
    a.size;
    -------------------------------------------------------------------------------------------------------- */
    ///////////////////////////////////////////////////////////////////////////////////////////////////////////
    // PUBLIC INSTANCE PROPERTIES
    ///////////////////////////////////////////////////////////////////////////////////////////////////////////
    /* --------------------------------------------------------------------------------------------------------
    • at
    
    Gets item at index.

    Returns item.

    a = FoscTypedSequenceableCollection([1, 2, 3, 4], Number);
    a[2];
    -------------------------------------------------------------------------------------------------------- */
    at { |index|
        if (index.isArray) { ^this.atAll(index) };
        ^collection.at(index);
    }
    /* --------------------------------------------------------------------------------------------------------
    • atAll
    
    Gets items at indices.

    Returns items.

    a = FoscTypedSequenceableCollection([1, 2, 3, 4], Number);
    a[(2..3)];
    -------------------------------------------------------------------------------------------------------- */
    atAll { |indices|
        ^collection.atAll(indices);
    }
    /* --------------------------------------------------------------------------------------------------------
    • copySeries

    a = FoscTypedSequenceableCollection([1, 2, 3, 4], Number);
    a[1..2];

    a = FoscTypedSequenceableCollection([1, 2, 3, 4], Number);
    a[1..];

    a = FoscTypedSequenceableCollection([1, 2, 3, 4], Number);
    a[..2];

    a = FoscTypedSequenceableCollection([1, 2, 3, 4, 5, 6, 7], Number);
    a.copySeries(0, 2);
    -------------------------------------------------------------------------------------------------------- */
    copySeries { |first, second, last|
        ^collection.copySeries(first, second, last);
    }
    /* --------------------------------------------------------------------------------------------------------
    • includes
    
    Answer true if item exists in collection.

    Returns boolean.

    a = FoscTypedSequenceableCollection([1, 2, 3, 4], Number);
    a.includes(3);
    a.includes(5);
    -------------------------------------------------------------------------------------------------------- */
    /* --------------------------------------------------------------------------------------------------------
    • indexOf (abjad: index)
    
    Return the first index matching item.

    Returns integer.

    a = FoscTypedSequenceableCollection([1, 2, 3, 4], Number);
    a.indexOf(3);
    -------------------------------------------------------------------------------------------------------- */
    indexOf { |item|
        item = this.prItemCoercer(item);
        ^collection.indexOf(item);
    }
    /* --------------------------------------------------------------------------------------------------------
    • last

    Return the last item.

    a = FoscTypedSequenceableCollection([1, 2, 3, 4], Number);
    a.last;
    -------------------------------------------------------------------------------------------------------- */
    last {
        ^collection.last;
    }
    /* --------------------------------------------------------------------------------------------------------
    • occurrencesOf (abjad: count)
    
    Return the number of occurrences of item in collection.

    Returns integer.

    a = FoscTypedSequenceableCollection([1, 2, 3, 4], Number);
    a.occurrencesOf(3);
    -------------------------------------------------------------------------------------------------------- */
    occurrencesOf { |item|
        item = this.prItemCoercer(item);
        ^collection.occurrencesOf(item);
    }
    ///////////////////////////////////////////////////////////////////////////////////////////////////////////
    // PUBLIC METHODS: LIST MODIFICATION
    ///////////////////////////////////////////////////////////////////////////////////////////////////////////
    /* --------------------------------------------------------------------------------------------------------
    • pop

    Removes last item.

    Returns removed item;

    a = FoscTypedSequenceableCollection([1, 2, 3, 4], Number);
    a.pop;
    a.inspect;
    -------------------------------------------------------------------------------------------------------- */
    pop {
        var oldItem;
        oldItem = collection.pop;
        this.prOnRemoval(oldItem);
        ^oldItem;
    }
    /* --------------------------------------------------------------------------------------------------------
    • remove

    Remove item.

    Returns removed item.

    a = FoscTypedSequenceableCollection([1, 2, 3, 4], Number);
    a.remove(2);
    a.items;
    -------------------------------------------------------------------------------------------------------- */
    remove { |item|
        item = this.prItemCoercer(item);
        this.prOnRemoval(item);
        ^collection.remove(item);
    }
    /* --------------------------------------------------------------------------------------------------------
    • removeAt

    Remove item at index.

    Returns removed item.

    a = FoscTypedSequenceableCollection([1, 2, 3, 4], Number);
    a.removeAt(1);
    a.items;
    -------------------------------------------------------------------------------------------------------- */
    removeAt { |index|
        ^collection.removeAt(index);
    }
    ///////////////////////////////////////////////////////////////////////////////////////////////////////////
    // PUBLIC METHODS: TRANSFORMATION
    ///////////////////////////////////////////////////////////////////////////////////////////////////////////
    /* --------------------------------------------------------------------------------------------------------
    • collect

    x = FoscTypedSequenceableCollection([1, 2, 3, 4], Number);
    x = x.collect { |each| each * 2 };
    x.inspect;
    -------------------------------------------------------------------------------------------------------- */
    collect { |func|
        var items;
        items = this.items.collect(func);
        ^this.species.new(items, itemClass);
    }
    /* --------------------------------------------------------------------------------------------------------
    • reject

    x = FoscTypedSequenceableCollection([1, 2, 3, 4], Number);
    x = x.reject { |each| each.even };
    x.inspect;
    -------------------------------------------------------------------------------------------------------- */
    reject { |func|
        var items;
        items = this.items.reject(func);
        ^this.species.new(items, itemClass);
    }
    /* --------------------------------------------------------------------------------------------------------
    • select

    x = FoscTypedSequenceableCollection([1, 2, 3, 4], Number);
    x = x.select { |each| each.even };
    x.inspect;
    -------------------------------------------------------------------------------------------------------- */
    select { |func|
        var items;
        items = this.items.select(func);
        ^this.species.new(items, itemClass);
    }
}
