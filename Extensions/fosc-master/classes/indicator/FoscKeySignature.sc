/* ------------------------------------------------------------------------------------------------------------
• FoscKeySignature

Key Signature.


• Example 1

a = FoscStaff([FoscNote(64, 1/4)]);
a[0].attach(FoscKeySignature("e", "major"));
a.show;
------------------------------------------------------------------------------------------------------------ */
FoscKeySignature : Fosc {
    ///////////////////////////////////////////////////////////////////////////////////////////////////////////
    // INIT
    ///////////////////////////////////////////////////////////////////////////////////////////////////////////
    var <tonic, <mode;
    var <context='Staff', <formatSlot='opening', <parameter=true, <redraw=true;
    *new { |tonic="c", mode="major"|
        ^super.new.init(tonic, mode);
    }
    init { |argTonic, argMode|
        argTonic = argTonic.asString;
        argMode = argMode.asString;

        if (FoscPitchManager.isPitchName(argTonic).not) {
            throw("FoscKeySignature:new: 'tonic' must be a valid pitch name: \"%\"".format(argTonic));
        };

       if (#["major", "minor"].includesEqual(argMode).not) {
            throw("FoscKeySignature:new: 'mode' must be \"major\" or \"minor\": \"%\"".format(argMode));
        };

        tonic = argTonic;
        mode = argMode;
    }
    ///////////////////////////////////////////////////////////////////////////////////////////////////////////
    // PUBLIC INSTANCE PROPERTIES
    ///////////////////////////////////////////////////////////////////////////////////////////////////////////
    /* --------------------------------------------------------------------------------------------------------
    • context

    Gets context. 'Staff' by default.
    

    • Example 1   

    a = FoscKeySignature("e", "major");
    a.context;
    -------------------------------------------------------------------------------------------------------- */
    /* --------------------------------------------------------------------------------------------------------
    • redraw

    Is true.


    • Example 1   

    a = FoscKeySignature("e", "major");
    a.redraw;
    -------------------------------------------------------------------------------------------------------- */
    /* --------------------------------------------------------------------------------------------------------
    • tweaks
    -------------------------------------------------------------------------------------------------------- */
    tweaks {
        // pass
    }
    ///////////////////////////////////////////////////////////////////////////////////////////////////////////
    // PUBLIC INSTANCE METHODS
    ///////////////////////////////////////////////////////////////////////////////////////////////////////////
    /* --------------------------------------------------------------------------------------------------------
    • format

    Formats key signature.

    Returns string.


    • Example 1

    a = FoscKeySignature("e", "major");
    a.format;
    -------------------------------------------------------------------------------------------------------- */
    format {
        ^this.prGetLilypondFormat;
    }
    ///////////////////////////////////////////////////////////////////////////////////////////////////////////
    // PRIVATE INSTANCE METHODS
    ///////////////////////////////////////////////////////////////////////////////////////////////////////////
    /* --------------------------------------------------------------------------------------------------------
    • prGetLilypondFormat
    
    a = FoscKeySignature("e", "major");
    a.prGetLilypondFormat;
    -------------------------------------------------------------------------------------------------------- */
    prGetLilypondFormat {
        ^"\\key % %".format(tonic, "\\" ++ mode);
    }
    /* --------------------------------------------------------------------------------------------------------
    • prGetLilypondFormatBundle
    -------------------------------------------------------------------------------------------------------- */
    prGetLilypondFormatBundle {
        var bundle;
        
        bundle = FoscLilyPondFormatBundle();
        bundle.opening.commands.add(this.prGetLilypondFormat);
        
        ^bundle;
    }
}
